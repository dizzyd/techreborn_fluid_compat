plugins {
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'maven-publish'
	id "com.matthewprenger.cursegradle" version "1.1.2"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	// Libblockattributes repo
	maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Depends on both LBA and TechReborn (since that's the whole point)
 	modApi "alexiil.mc.lib:libblockattributes-all:${project.lba_version}"
	modApi ('TechReborn:TechReborn-1.17:5.0.6-beta+build.94') { exclude group: "net.fabricmc.fabric-api"}
	modApi ('RebornCore:RebornCore-1.17:5.0.6-beta+build.94') { exclude group: "net.fabricmc.fabric-api"}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.17 (21w19a) upwards uses Java 16.
	it.options.release = 16
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		maven {
			url = "s3://files.mcdex.net/maven2"
			credentials(AwsCredentials) {
				accessKey = System.getenv("AWS_ACCESS_KEY_ID")
				secretKey = System.getenv("AWS_SECRET_ACCESS_KEY")
			}
		}
	}
}

curseforge {
	apiKey = System.getenv("CURSEFORGE_API_KEY")

	project {
		id = "522258"
		addGameVersion "1.17.1"
		releaseType = 'alpha'
		mainArtifact(remapJar) {
			displayName = "Tech Reborn Fluid Compatibility $project.version"
		}
		changelog = "Changes"
	}
	options {
		forgeGradleIntegration = false
	}
}

afterEvaluate {
	tasks.curseforge.dependsOn remapJar
}